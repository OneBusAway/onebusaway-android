/*
 * Copyright (C) 2013-2017 University of South Florida (sjbarbeau@gmail.com),
 * Microsoft Corporation.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.filters.ConcatFilter

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

repositories {
    maven {
        // OBA Releases - for comparator to sort alphanumeric routes
        url "http://nexus.onebusaway.org/nexus/content/repositories/releases"
    }
    maven {
        // for Amazon Maps
        url uri('../.m2/repository')
    }
    maven {
        // CUTR Releases
        url "https://github.com/CUTR-at-USF/cutr-mvn-repo/raw/master/snapshots"
    }
    mavenCentral()
}

android {
    dexOptions {
        preDexLibraries true
        javaMaxHeapSize "3g"
    }
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 122
        versionName "2.8.0"

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // This enables us to tell when we're running unit tests on Travis (#1010)
        buildConfigField("String", "TRAVIS", "\"" + System.getenv('TRAVIS') + "\"")
    }

    /**
     * brand - how the name, look, and feel of each OneBusAway-based app is presented
     * platform - Google or Amazon (for Maps API v2 dependencies)
     */
    flavorDimensions "brand", "platform"

    productFlavors {
        google {
            // Normal Google Play release - see src/google
            dimension "platform"
            isDefault = true
        }

        amazon {
            /**
             * Amazon Fire Phone - see src/amazon
             *
             * NOTE: The "copyMapsApiV2Classes" task automatically copies the classes from
             * src/google/java/org/onebusaway/android/map package to the
             * src/amazon/java/org/onebusaway/android/map package (with the exception of
             * ProprietaryMapHelpV2) when the project is built, and replaces the imports and
             * GoogleMap class with the Amazon equivalents.  Therefore, the files in
             * src/amazon/java/org/onebusaway/android/map should not be manually changed.
             */
            dimension "platform"
        }

        oba {
            /**
             * Original OneBusAway brand
             *
             * To allow this app to be distributed via Google Play, the applicationId MUST remain the
             * original package name of the app before it was re-branded to OneBusAway:
             * com.joulespersecond.seattlebusbot
             */
            dimension "brand"
            isDefault = true
            applicationId "com.joulespersecond.seattlebusbot"
            manifestPlaceholders = [databaseAuthority: "com.joulespersecond.oba"]
            buildConfigField "int", "ARRIVAL_INFO_STYLE", "0" // Defined in BuildFlavorConstants
            buildConfigField "boolean", "USE_FIXED_REGION", "false" // Supports multi-region
            buildConfigField "boolean", "USE_PELIAS_GEOCODING", "true"
            buildConfigField "String", "PELIAS_API_KEY", "\"" + getPeliasKey(name) + "\""
            // Below fields need to be defined so flavor builds, but will not be used by app
            buildConfigField "String", "FIXED_REGION_NAME", "null"
            buildConfigField "String", "FIXED_REGION_OBA_BASE_URL", "null"
            buildConfigField "String", "FIXED_REGION_SIRI_BASE_URL", "null"
            buildConfigField "double", "FIXED_REGION_BOUNDS_LAT", "0"
            buildConfigField "double", "FIXED_REGION_BOUNDS_LON", "0"
            buildConfigField "double", "FIXED_REGION_BOUNDS_LAT_SPAN", "0"
            buildConfigField "double", "FIXED_REGION_BOUNDS_LON_SPAN", "0"
            buildConfigField "String", "FIXED_REGION_LANG", "null"
            buildConfigField "String", "FIXED_REGION_CONTACT_EMAIL", "null"
            buildConfigField "boolean", "FIXED_REGION_SUPPORTS_OBA_DISCOVERY_APIS", "true"
            buildConfigField "boolean", "FIXED_REGION_SUPPORTS_OBA_REALTIME_APIS", "true"
            buildConfigField "boolean", "FIXED_REGION_SUPPORTS_SIRI_REALTIME_APIS", "false"
            buildConfigField "String", "FIXED_REGION_TWITTER_URL", "null"
            buildConfigField "String", "FIXED_REGION_STOP_INFO_URL", "null"
            buildConfigField "String", "FIXED_REGION_OPEN311_JURISDICTION_ID", "null"
            buildConfigField "String", "FIXED_REGION_OPEN311_API_KEY", "null"
            buildConfigField "String", "FIXED_REGION_OPEN311_BASE_URL", "null"
            buildConfigField "String", "FIXED_REGION_OTP_BASE_URL", "null"
            buildConfigField "String", "FIXED_REGION_OTP_CONTACT_EMAIL", "null"
            buildConfigField "boolean", "FIXED_REGION_SUPPORTS_OTP_BIKESHARE", "false"
            buildConfigField "String", "FIXED_REGION_PAYMENT_ANDROID_APP_ID", "null"
            buildConfigField "String", "FIXED_REGION_PAYMENT_WARNING_TITLE", "null"
            buildConfigField "String", "FIXED_REGION_PAYMENT_WARNING_BODY", "null"
            buildConfigField "boolean", "FIXED_REGION_TRAVEL_BEHAVIOR_DATA_COLLECTION", "false"
            buildConfigField "boolean", "FIXED_REGION_ENROLL_PARTICIPANTS_IN_STUDY", "false"
        }

        /**
         * Rebranded versions of OBA using brand build flavors are below.  To add a new brand:
         *  1. Add a new brand build flavor variant below
         *  2. Create a new folder src/{brandName}/res (or copy from one of the samples - src/agencyX or src/agencyY)
         *  3. Edit resource files in src/{brandName}/res subfolders with the brand information
         *
         *  For generating icons for the res/mipmap-* folders, see http://romannurik.github.io/AndroidAssetStudio/icons-launcher.html
         */

        agencyX {
            // "Agency X" rebranding OneBusAway as another app - see src/agencyX/res for branded resources
            dimension "brand"
            applicationId "org.agencyx.android" // Unique listing of this brand on app store
            manifestPlaceholders = [databaseAuthority: applicationId.toString() + '.provider']
            buildConfigField "int", "ARRIVAL_INFO_STYLE", "0"  // Defined in BuildFlavorConstants
            buildConfigField "boolean", "USE_FIXED_REGION", "false" // Supports multi-region
            buildConfigField "boolean", "USE_PELIAS_GEOCODING", "true"
            buildConfigField "String", "PELIAS_API_KEY", "\"" + getPeliasKey(name) + "\""
            // Below fields need to be defined so flavor builds, but will not be used by app
            buildConfigField "String", "FIXED_REGION_NAME", "null"
            buildConfigField "String", "FIXED_REGION_OBA_BASE_URL", "null"
            buildConfigField "String", "FIXED_REGION_SIRI_BASE_URL", "null"
            buildConfigField "double", "FIXED_REGION_BOUNDS_LAT", "0"
            buildConfigField "double", "FIXED_REGION_BOUNDS_LON", "0"
            buildConfigField "double", "FIXED_REGION_BOUNDS_LAT_SPAN", "0"
            buildConfigField "double", "FIXED_REGION_BOUNDS_LON_SPAN", "0"
            buildConfigField "String", "FIXED_REGION_LANG", "null"
            buildConfigField "String", "FIXED_REGION_CONTACT_EMAIL", "null"
            buildConfigField "boolean", "FIXED_REGION_SUPPORTS_OBA_DISCOVERY_APIS", "true"
            buildConfigField "boolean", "FIXED_REGION_SUPPORTS_OBA_REALTIME_APIS", "true"
            buildConfigField "boolean", "FIXED_REGION_SUPPORTS_SIRI_REALTIME_APIS", "false"
            buildConfigField "String", "FIXED_REGION_TWITTER_URL", "null"
            buildConfigField "String", "FIXED_REGION_STOP_INFO_URL", "null"
            buildConfigField "String", "FIXED_REGION_OPEN311_JURISDICTION_ID", "null"
            buildConfigField "String", "FIXED_REGION_OPEN311_API_KEY", "null"
            buildConfigField "String", "FIXED_REGION_OPEN311_BASE_URL", "null"
            buildConfigField "String", "FIXED_REGION_OTP_BASE_URL", "null"
            buildConfigField "String", "FIXED_REGION_OTP_CONTACT_EMAIL", "null"
            buildConfigField "boolean", "FIXED_REGION_SUPPORTS_OTP_BIKESHARE", "false"
            buildConfigField "String", "FIXED_REGION_PAYMENT_ANDROID_APP_ID", "null"
            buildConfigField "String", "FIXED_REGION_PAYMENT_WARNING_TITLE", "null"
            buildConfigField "String", "FIXED_REGION_PAYMENT_WARNING_BODY", "null"
            buildConfigField "boolean", "FIXED_REGION_TRAVEL_BEHAVIOR_DATA_COLLECTION", "false"
            buildConfigField "boolean", "FIXED_REGION_ENROLL_PARTICIPANTS_IN_STUDY", "false"
        }

        agencyY {
            // "Agency Y" rebranding OneBusAway as another app - see src/agencyY/res for branded resources
            dimension "brand"
            applicationId "org.agencyy.android" // Unique listing of this brand on app store
            manifestPlaceholders = [databaseAuthority: applicationId.toString() + '.provider']
            buildConfigField "int", "ARRIVAL_INFO_STYLE", "1" // Defined in BuildFlavorConstants
            buildConfigField "boolean", "USE_FIXED_REGION", "true" // Does not support multi-region
            buildConfigField "boolean", "USE_PELIAS_GEOCODING", "true"
            buildConfigField "String", "PELIAS_API_KEY", "\"" + getPeliasKey(name) + "\""
            // Fixed region info that the app will use
            buildConfigField "String", "FIXED_REGION_NAME", "\"Agency Y\""
            buildConfigField "String", "FIXED_REGION_OBA_BASE_URL",
                    "\"http://api.tampa.onebusaway.org/api\""
            buildConfigField "String", "FIXED_REGION_SIRI_BASE_URL", "null"
            buildConfigField "double", "FIXED_REGION_BOUNDS_LAT", "27.976910500000002"
            buildConfigField "double", "FIXED_REGION_BOUNDS_LON", "-82.445851"
            buildConfigField "double", "FIXED_REGION_BOUNDS_LAT_SPAN", "0.5424609999999994"
            buildConfigField "double", "FIXED_REGION_BOUNDS_LON_SPAN", "0.576357999999999"
            buildConfigField "String", "FIXED_REGION_LANG", "\"en_US\""
            buildConfigField "String", "FIXED_REGION_CONTACT_EMAIL", "\"onebusaway@gohart.org\""
            buildConfigField "boolean", "FIXED_REGION_SUPPORTS_OBA_DISCOVERY_APIS", "true"
            buildConfigField "boolean", "FIXED_REGION_SUPPORTS_OBA_REALTIME_APIS", "true"
            buildConfigField "boolean", "FIXED_REGION_SUPPORTS_SIRI_REALTIME_APIS", "false"
            buildConfigField "String", "FIXED_REGION_TWITTER_URL",
                    "\"http://mobile.twitter.com/OBA_tampa\""
            buildConfigField "String", "FIXED_REGION_STOP_INFO_URL", "null"
            buildConfigField "String", "FIXED_REGION_OPEN311_JURISDICTION_ID", "null"
            buildConfigField "String", "FIXED_REGION_OPEN311_API_KEY", "\"937033cad3054ec58a1a8156dcdd6ad8a416af2f\""
            buildConfigField "String", "FIXED_REGION_OPEN311_BASE_URL",
                    "\"https://seeclickfix.com/open311/v2/\""
            buildConfigField "String", "FIXED_REGION_OTP_BASE_URL",
                    "\"http://otp.staging.obahart.org/otp/\""
            buildConfigField "String", "FIXED_REGION_OTP_CONTACT_EMAIL",
                    "\"otp-tampa@onebusaway.org\""
            buildConfigField "boolean", "FIXED_REGION_SUPPORTS_OTP_BIKESHARE", "true"
            buildConfigField "String", "FIXED_REGION_PAYMENT_ANDROID_APP_ID", "\"co.bytemark.hart\""
            buildConfigField "String", "FIXED_REGION_PAYMENT_WARNING_TITLE", "null"
            buildConfigField "String", "FIXED_REGION_PAYMENT_WARNING_BODY", "null"
            buildConfigField "boolean", "FIXED_REGION_TRAVEL_BEHAVIOR_DATA_COLLECTION", "false"
            buildConfigField "boolean", "FIXED_REGION_ENROLL_PARTICIPANTS_IN_STUDY", "false"
        }
        yrt {
            // "YRT - York Region Transit - Canada" rebranding - https://play.google.com/store/apps/details?id=can.yrt.onebusaway
            dimension "brand"
            applicationId "can.yrt.onebusaway" // Unique listing of this brand on app store
            manifestPlaceholders = [databaseAuthority: applicationId.toString() + '.provider']
            buildConfigField "int", "ARRIVAL_INFO_STYLE", "1" // Defined in BuildFlavorConstants
            buildConfigField "boolean", "USE_FIXED_REGION", "true" // Does not support multi-region
            buildConfigField "boolean", "USE_PELIAS_GEOCODING", "false"
            buildConfigField "String", "PELIAS_API_KEY", "\"" + getPeliasKey(name) + "\""
            // Fixed region info that the app will use
            buildConfigField "String", "FIXED_REGION_NAME", "\"YORK\""
            buildConfigField "String", "FIXED_REGION_OBA_BASE_URL",
                    "\"http://oba.yrt.ca/\""
            buildConfigField "String", "FIXED_REGION_SIRI_BASE_URL", "null"
            buildConfigField "double", "FIXED_REGION_BOUNDS_LAT", "44.011994"
            buildConfigField "double", "FIXED_REGION_BOUNDS_LON", "-79.47573600000001"
            buildConfigField "double", "FIXED_REGION_BOUNDS_LAT_SPAN", "0.6502339999999975"
            buildConfigField "double", "FIXED_REGION_BOUNDS_LON_SPAN", "0.5733579999999989"
            buildConfigField "String", "FIXED_REGION_LANG", "\"en_US\""
            buildConfigField "String", "FIXED_REGION_CONTACT_EMAIL", "\"transitinfo@york.ca\""
            buildConfigField "boolean", "FIXED_REGION_SUPPORTS_OBA_DISCOVERY_APIS", "true"
            buildConfigField "boolean", "FIXED_REGION_SUPPORTS_OBA_REALTIME_APIS", "true"
            buildConfigField "boolean", "FIXED_REGION_SUPPORTS_SIRI_REALTIME_APIS", "false"
            buildConfigField "String", "FIXED_REGION_TWITTER_URL",
                    "\"https://twitter.com/YRTViva\""
            buildConfigField "String", "FIXED_REGION_STOP_INFO_URL", "null"
            buildConfigField "String", "FIXED_REGION_OPEN311_JURISDICTION_ID", "null"
            buildConfigField "String", "FIXED_REGION_OPEN311_API_KEY", "null"
            buildConfigField "String", "FIXED_REGION_OPEN311_BASE_URL", "null"
            buildConfigField "String", "FIXED_REGION_OTP_BASE_URL",
                    "\"https://otp.yrt.ca/otp/\""
            buildConfigField "String", "FIXED_REGION_OTP_CONTACT_EMAIL",
                    "\"transitinfo@york.ca\""
            buildConfigField "boolean", "FIXED_REGION_SUPPORTS_OTP_BIKESHARE", "false"
            buildConfigField "String", "FIXED_REGION_PAYMENT_ANDROID_APP_ID", "\"co.bytemark.york\""
            buildConfigField "String", "FIXED_REGION_PAYMENT_WARNING_TITLE", "null"
            buildConfigField "String", "FIXED_REGION_PAYMENT_WARNING_BODY", "null"
            buildConfigField "boolean", "FIXED_REGION_TRAVEL_BEHAVIOR_DATA_COLLECTION", "false"
            buildConfigField "boolean", "FIXED_REGION_ENROLL_PARTICIPANTS_IN_STUDY", "false"
        }
        /**
         * Add more rebranding flavors here...
         */
    }

    /**
     * Set brand BuildConfig.DATABASE_AUTHORITY, used in org.onebusaway.android.provider.ObaContract
     */
    applicationVariants.all {
        variant ->
            def authority;
            if (variant.productFlavors.get(0).name == "oba") {
                authority = '"' + "com.joulespersecond.oba" + '"'
                // Must keep the original OBA authority
                variant.buildConfigField "String", "DATABASE_AUTHORITY", authority
            } else {
                // It's cleaner to just append ".provider" to the applicationId for brand flavors
                authority = '"' + applicationId.toString() + '.provider' + '"'
                variant.buildConfigField "String", "DATABASE_AUTHORITY", authority
            }
    }

    // http://stackoverflow.com/questions/20673625/gradle-0-7-0-duplicate-files-during-packaging-of-apk
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        disable 'MissingTranslation', 'ExtraTranslation'
    }

    if (project.hasProperty("secure.properties")
            && new File(project.property("secure.properties")).exists()) {

        Properties props = new Properties()
        props.load(new FileInputStream(file(project.property("secure.properties"))))

        signingConfigs {
            debug {
                storeFile file("seattlebusbot3.debug.keystore")
            }

            release {
                storeFile file(props['key.store'])
                keyAlias props['key.alias']
                storePassword props['key.storepassword']
                keyPassword props['key.keypassword']
            }
        }
    } else {
        signingConfigs {
            debug {
                storeFile file("seattlebusbot3.debug.keystore")
            }

            release {
                // Nothing here
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
        // Append the version name to the end of aligned APKs
        android.applicationVariants.all { variant ->
            if (variant.buildType.name == "release") {
                variant.outputs.all { output ->
                    outputFileName = "${variant.name}-v${variant.versionName}.apk"
                }
            }
        }
    }

    // Filter out the Amazon release build variants for AgencyX and AgencyY brands because they are unused
    variantFilter { variant ->
        def names = variant.flavors*.name
        def brand = names.get(0)
        def platform = names.get(1)
        def isRelease = variant.buildType.name.equals('release')
        if (isRelease &&
                ((brand.equals("agencyX") && platform.equals("amazon")) ||
                        (brand.equals("agencyY") && platform.equals("amazon")) )) {
            variant.setIgnore(true)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.includeAndroidResources true
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

/**
 * Gets the Pelias geocoding API key for the given product flavor
 * @param productFlavor the product flavor being built
 * @return the Pelias geocoding API key for the given product flavor
 */
def getPeliasKey(productFlavor) {
    def keyName = "Pelias" + "_" + productFlavor
    if (project.hasProperty(keyName)) {
        return project.property(keyName)
    } else {
        return ""
    }
}

/**
 * Copies our Maps API v2-based classes from the Google build flavor to the Amazon build flavor,
 * replacing the Google import packages and GoogleMap class with the Amazon import packages and
 * AmazonMap class.  Copy is only executed if the files in the google build flavor are no longer
 * equal to the ones in the amazon build flavor.
 */
task copyMapsApiV2Classes {
    inputs.dir 'src/google/java/org/onebusaway/android/map/googlemapsv2'
    outputs.dir 'src/amazon/java/org/onebusaway/android/map/googlemapsv2'
    // up-to-date check for outputs
    doLast {
        copy {
            from 'src/google/java/org/onebusaway/android/map/googlemapsv2'
            into 'src/amazon/java/org/onebusaway/android/map/googlemapsv2'
            exclude { details -> details.file.name.equals('ProprietaryMapHelpV2.java') }
            filter { line -> line.replace('com.google.android.gms.maps', 'com.amazon.geo.mapsv2') }
            filter { line -> line.replace('GoogleMap', 'AmazonMap') }
            filter(ConcatFilter, prepend: file('AmazonMapsFileHeader.txt'))
        }
    }
}

preBuild.dependsOn copyMapsApiV2Classes

// Exclude all classes from dependencies that conflict with Android platform classes (#849)
configurations {
    all {
        exclude group: 'org.json', module: 'json'
    }
}

dependencies {
    // Firebase Analytics
    implementation 'com.google.firebase:firebase-core:16.0.8'
    implementation 'com.google.firebase:firebase-analytics:16.4.0'
    // Cloud Firestore (for storing destination alert test data)
    implementation 'com.google.firebase:firebase-firestore:17.1.4'
    implementation 'com.google.firebase:firebase-auth:16.1.0'
    implementation 'com.google.firebase:firebase-storage:16.0.5'
    // Google Play Services Location (we need this on Amazon flavor too)
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    // Support libraries
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'android.arch.lifecycle:common-java8:1.1.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'commons-io:commons-io:2.4'
    implementation 'org.apache.commons:commons-lang3:3.0'
    // Open311 client library
    implementation 'edu.usf.cutr:open311client:1.0.10'
    // JSON data binding for OBA REST API responses
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.4'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.4'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.4'
    // Bottom sliding panel
    implementation 'com.sothree.slidinguppanel:library:3.3.0'
    // For floating action button speed dial
    implementation ('uk.co.markormesher:android-fab:2.2.0')
    // For sorting alphanumeric route names
    implementation 'org.onebusaway.util:comparators:1.0.0'
    // For tutorial (ShowcaseView) library
    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'
    // POJOs used for full data-binding via Jackson
    implementation 'edu.usf.cutr.opentripplanner.android:opentripplanner-pojos:1.0.0-SNAPSHOT'
    // Pelias for point-of-interest search and geocoding for trip planning origin and destination
    implementation 'edu.usf.cutr:pelias-client-library:1.1.0'
    // Google Play Services Maps (only for Google flavor)
    googleImplementation 'com.google.android.gms:play-services-maps:16.1.0'
    // Google Play Services Places is required by ProprietaryMapHelpV2 (only for Google flavor)
    googleImplementation 'com.google.android.libraries.places:places-compat:1.1.0'
    // Amazon Maps (only for Amazon flavor)
    amazonImplementation 'com.amazon.android:amazon-maps-api:2.0'
    // Autocomplete text views with clear button for trip planning
    implementation 'com.google.android.material:material:1.1.0'
    // Required to support multidex for minSdkVersion <= 20
    implementation 'androidx.multidex:multidex:2.0.1'
    // Unit tests - seems like this is still necessary w/ Android X even though useLibrary is declared earlier
    androidTestImplementation 'androidx.test:runner:1.1.0'
    // WorkManager (Java only)
    implementation 'androidx.work:work-runtime:2.0.0'
    implementation "androidx.concurrent:concurrent-futures:1.0.0-beta01"
    implementation "androidx.concurrent:concurrent-listenablefuture:1.0.0-beta01"
    implementation "androidx.concurrent:concurrent-listenablefuture-callback:1.0.0-beta01"
    implementation "androidx.core:core-ktx:1.3.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

}

apply plugin:'com.google.gms.google-services'
