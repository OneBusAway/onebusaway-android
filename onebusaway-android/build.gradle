/*
 * Copyright (C) 2013-2017 University of South Florida (sjbarbeau@gmail.com),
 * Microsoft Corporation.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.filters.ConcatFilter

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

repositories {
    maven {
        // OBA Releases - for comparator to sort alphanumeric routes
        url "https://repo.camsys-apps.com/releases"
    }
    maven {
        // CUTR Releases
        url "https://github.com/CUTR-at-USF/cutr-mvn-repo/raw/master/snapshots"
    }
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

android {
    compileSdk 36

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 36
        versionCode 153
        versionName "2.14.9"

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // This enables us to tell when we're running unit tests on CI (#1010 for Travis, #1072 for GitHub)
        buildConfigField("String", "CI", "\"" + System.getenv('CI') + "\"")

        // Configure Java compile options to specify the schema location for Room database to keep track of schema versions
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    /**
     * brand - how the name, look, and feel of each OneBusAway-based app is presented
     * platform - Google
     */
    flavorDimensions = ["brand", "platform"]

    productFlavors {
        google {
            // Normal Google Play release - see src/google
            dimension "platform"
            isDefault = true
        }

        // Brand flavors are loaded from separate files in flavors/ directory.
        // See flavors/README.md for instructions on adding new white-label brands.
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/NOTICE']
        }
    }

    /**
     * Set brand BuildConfig.DATABASE_AUTHORITY, used in org.onebusaway.android.provider.ObaContract
     */
    applicationVariants.all {
        variant ->
            def authority;
            if (variant.productFlavors.get(0).name == "oba") {
                authority = '"' + "com.joulespersecond.oba" + '"'
                // Must keep the original OBA authority
                variant.buildConfigField "String", "DATABASE_AUTHORITY", authority
            } else {
                // It's cleaner to just append ".provider" to the applicationId for brand flavors
                authority = '"' + applicationId.toString() + '.provider' + '"'
                variant.buildConfigField "String", "DATABASE_AUTHORITY", authority
            }
    }

    // http://stackoverflow.com/questions/20673625/gradle-0-7-0-duplicate-files-during-packaging-of-apk


    if (project.hasProperty("secure.properties")
            && new File(project.property("secure.properties")).exists()) {

        Properties props = new Properties()
        props.load(new FileInputStream(file(project.property("secure.properties"))))

        signingConfigs {
            debug {
                storeFile file("seattlebusbot3.debug.keystore")
            }

            release {
                storeFile file(props['key.store'])
                keyAlias props['key.alias']
                storePassword props['key.storepassword']
                keyPassword props['key.keypassword']
            }
        }
    } else {
        signingConfigs {
            debug {
                storeFile file("seattlebusbot3.debug.keystore")
            }

            release {
                // Nothing here
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
        // Append the version name to the end of aligned APKs
        android.applicationVariants.all { variant ->
            if (variant.buildType.name == "release") {
                variant.outputs.all { output ->
                    outputFileName = "${variant.name}-v${variant.versionName}.apk"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    testOptions {
        unitTests.includeAndroidResources true
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
    namespace 'org.onebusaway.android'
    lint {
        disable 'MissingTranslation', 'ExtraTranslation'
    }
}

// Load brand flavor configurations from flavors/ directory
// See flavors/README.md for instructions on adding new white-label brands
apply from: "flavors/load-flavors.gradle"

/**
 * Gets the Pelias geocoding API key for the given product flavor
 * @param productFlavor the product flavor being built
 * @return the Pelias geocoding API key for the given product flavor
 */
def getPeliasKey(productFlavor) {
    def keyName = "Pelias" + "_" + productFlavor
    if (project.hasProperty(keyName)) {
        return project.property(keyName)
    } else {
        return ""
    }
}

// Exclude all classes from dependencies that conflict with Android platform classes (#849)
configurations {
    all {
        exclude group: 'org.json', module: 'json'
    }
}

dependencies {
    // Firebase Analytics
    implementation 'com.google.firebase:firebase-core:21.1.1'
    implementation 'com.google.firebase:firebase-analytics:22.1.2'
    // Plausible Analytics
    implementation 'com.github.OneBusAway:plausible-android-sdk:3.3'
    // Cloud Firestore (for storing destination alert test data)
    implementation('com.google.firebase:firebase-firestore:25.1.0') {
        // Exclude protobuf-lite and protolite-well-known-types to avoid conflicts with GTFS-realtime bindings
        // See https://github.com/firebase/firebase-android-sdk/issues/5997
        exclude group: 'com.google.firebase', module: 'protolite-well-known-types'
        exclude group: 'com.google.protobuf', module: 'protobuf-javalite'
    }
    implementation 'com.google.firebase:firebase-auth:21.0.5'
    implementation 'com.google.firebase:firebase-storage:21.0.1'
    // Firebase Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics:19.2.0'
    implementation 'com.google.firebase:firebase-crashlytics-buildtools:3.0.2'
    // Google Play Services Location
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    // OneSignal
    implementation 'com.onesignal:OneSignal:[5.0.0, 5.99.99]'
    // Support libraries
    implementation 'androidx.activity:activity-ktx:1.9.3'
    implementation 'androidx.fragment:fragment-ktx:1.8.4'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'commons-io:commons-io:2.4'
    implementation 'org.apache.commons:commons-lang3:3.0'
    // Open311 client library
    implementation 'com.github.OneBusAway:open311-client:1.0.10'
    // JSON data binding for OBA REST API responses
    implementation 'com.fasterxml.jackson.core:jackson-core:2.12.4'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.4'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.4'
    // Bottom sliding panel
    implementation 'com.github.hannesa2:AndroidSlidingUpPanel:4.6.1'
    // For floating action button speed dial
    implementation 'com.github.OneBusAway:android-fab:2.4.1'
    // For sorting alphanumeric route names
    implementation 'org.onebusaway.util:comparators:1.0.0'
    // For tutorial (ShowcaseView) library
    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'
    // POJOs used for full data-binding via Jackson
    implementation 'edu.usf.cutr.opentripplanner.android:opentripplanner-pojos:1.0.0-SNAPSHOT'
    // Pelias for point-of-interest search and geocoding for trip planning origin and destination
    implementation 'com.github.OneBusAway:pelias-client-library:v1.1.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    // Google Play Services Maps (only for Google flavor)
    googleImplementation 'com.google.android.gms:play-services-maps:19.0.0'
    // Google Play Services Places is required by ProprietaryMapHelpV2 (only for Google flavor)
    googleImplementation 'com.google.android.libraries.places:places-compat:2.6.0'
    // Autocomplete text views with clear button for trip planning
    implementation 'com.google.android.material:material:1.12.0'
    // Required to support multidex for minSdkVersion <= 20
    implementation 'androidx.multidex:multidex:2.0.1'
    // Explicit dependency on Gson is now apparently needed
    implementation 'com.google.code.gson:gson:2.10.1'
    // Unit tests - seems like this is still necessary w/ Android X even though useLibrary is declared earlier
    androidTestImplementation 'androidx.test:runner:1.6.2'
    // WorkManager (Java only)
    implementation 'androidx.work:work-runtime:2.9.1'
    implementation "androidx.concurrent:concurrent-futures:1.2.0"
    implementation "androidx.concurrent:concurrent-listenablefuture:1.0.0-beta01"
    implementation "androidx.concurrent:concurrent-listenablefuture-callback:1.0.0-beta01"
    implementation "androidx.core:core-ktx:1.13.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // RoomDB
    implementation "androidx.room:room-runtime:2.6.1"
    kapt "androidx.room:room-compiler:2.6.1"
    implementation "androidx.room:room-ktx:2.6.1"
    // GTFS Realtime bindings for parsing GTFS-realtime data
    implementation group: 'org.mobilitydata', name: 'gtfs-realtime-bindings', version: '0.0.8'
    implementation 'com.google.api.grpc:proto-google-common-protos:2.9.0'
}

apply plugin:'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

/**
 * Validates that strings with %1$s placeholders in the base English strings.xml
 * also have the placeholder in all translation files. This prevents runtime crashes
 * from mismatched format strings in white-label brands.
 *
 * Run with: ./gradlew validateStringPlaceholders
 */
tasks.register('validateStringPlaceholders') {
    group = 'verification'
    description = 'Validates placeholder consistency across all string translations'

    doLast {
        def baseStringsFile = file("src/main/res/values/strings.xml")
        if (!baseStringsFile.exists()) {
            throw new GradleException("Base strings.xml not found at ${baseStringsFile}")
        }

        // Parse base strings and find those with %1$s placeholder
        def baseXml = new XmlSlurper().parse(baseStringsFile)
        def stringsWithPlaceholder = [:]
        baseXml.string.each { stringNode ->
            def name = stringNode.@name.toString()
            def value = stringNode.text()
            if (value.contains('%1$s')) {
                stringsWithPlaceholder[name] = value
            }
        }

        println "Found ${stringsWithPlaceholder.size()} strings with %1\$s placeholder in base strings.xml"

        // Check each translation directory
        def errors = []
        def translationDirs = fileTree("src/main/res") {
            include "values-*/strings.xml"
        }

        translationDirs.each { transFile ->
            def locale = transFile.parentFile.name.replace("values-", "")
            def transXml = new XmlSlurper().parse(transFile)

            stringsWithPlaceholder.each { name, baseValue ->
                def transNode = transXml.string.find { it.@name.toString() == name }
                if (transNode != null) {
                    def transValue = transNode.text()
                    if (!transValue.contains('%1$s')) {
                        errors << "[$locale] String '$name' is missing %1\$s placeholder"
                    }
                }
                // Note: Missing translations are OK (they fall back to English)
            }
        }

        if (errors) {
            println "\n❌ Found ${errors.size()} placeholder consistency errors:\n"
            errors.each { println "  - $it" }
            println "\nThese strings have %1\$s in English but not in translations."
            println "This can cause runtime crashes. Please add the placeholder to the translations."
            throw new GradleException("String placeholder validation failed")
        } else {
            println "✓ All translations have consistent placeholders"
        }
    }
}
